#version 460
#pragma shader_stage(compute)

layout(local_size_x = 8, local_size_y = 8) in;

#include "skyTypes.glsl"

#define U_ATMO u_atmo
#define S_TRANSMITTANCE s_transmittance
#define S_MULTISCATTERING s_multiscattering

layout(binding = 0) uniform Atmosphere {
	AtmosphereParams U_ATMO;
};
layout(binding = 1) uniform sampler2D S_TRANSMITTANCE;
layout(binding = 2) uniform sampler2D S_MULTISCATTERING;
layout(binding = 3) restrict writeonly uniform image2D i_view;

layout(push_constant) uniform Constants {
	vec3 probePos;
	float _pad0;
	vec3 sunDirection;
	float _pad1;
	vec3 sunIlluminance;
} C;

layout(constant_id = 0) const int ViewSizeWidth = 0;
layout(constant_id = 1) const int ViewSizeHeight = 0;
const uvec2 SkyViewSize = uvec2(ViewSizeWidth, ViewSizeHeight);

#include "sky.glsl"

void main() {
	const uvec2 Gid = gl_GlobalInvocationID.xy;

	if (any(greaterThanEqual(Gid, SkyViewSize))) return;

	//TODO which?
	// vec2 pixPos = vec2(Gid) + vec2(0.5);
	vec2 pixPos = vec2(Gid);
	vec3 worldPos = C.probePos + vec3(0, 0, u_atmo.bottomRadius);
	vec2 uv = pixPos / vec2(SkyViewSize);

	float viewHeight = length(worldPos);

	float viewZenithCosAngle;
	float lightViewCosAngle;
	uvToSkyViewLutParams(viewZenithCosAngle, lightViewCosAngle, SkyViewSize, viewHeight, uv, u_atmo.bottomRadius);

	vec3 upVector = worldPos / viewHeight;
	float sunZenithCosAngle = dot(upVector, C.sunDirection);
	vec3 sunDir = normalize(vec3(sqrt(1.0 - sunZenithCosAngle * sunZenithCosAngle), 0.0, sunZenithCosAngle));

	worldPos = vec3(0.0, 0.0, viewHeight);

	float viewZenithSinAngle = sqrt(1 - viewZenithCosAngle * viewZenithCosAngle);
	vec3 worldDir = vec3(
		viewZenithSinAngle * lightViewCosAngle,
		viewZenithSinAngle * sqrt(1.0 - lightViewCosAngle * lightViewCosAngle),
		viewZenithCosAngle);

	if (!moveToTopAtmosphere(worldPos, worldDir, u_atmo.topRadius)) {
		// Ray is not intersecting the atmosphere
		imageStore(i_view, ivec2(Gid), vec4(0.0, 0.0, 0.0, 1.0));
		return;
	}

	bool ground = false;
	float sampleCountIni = 30;
	bool variableSampleCount = true;
	bool mieRayPhase = true;
	SingleScatteringResult ss = integrateScatteredLuminance(worldPos, worldDir, sunDir,
		ground, sampleCountIni, variableSampleCount, mieRayPhase, 9000000.0, C.sunIlluminance);

	vec3 L = ss.L;
	imageStore(i_view, ivec2(Gid), vec4(L, 1.0));
}
